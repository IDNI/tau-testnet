plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
}

android {
    namespace 'org.tau.wallet'
    compileSdk 34

    defaultConfig {
        applicationId "org.tau.wallet"
        minSdk 24
        targetSdk 34
        versionCode 1
        versionName "0.1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // Pass BLST_DIR to CMake
        def props = new Properties()
        def lp = rootProject.file('local.properties')
        if (lp.exists()) {
            lp.withInputStream { props.load(it) }
        }
        def blstDir = props.getProperty('blst.dir', System.getenv('BLST_DIR') ?: '/Users/ak/idni/blst')
        externalNativeBuild {
            cmake {
                arguments "-DBLST_DIR=${blstDir}"
            }
        }

        // Target common ABIs; adjust as needed
        ndk {
            abiFilters 'arm64-v8a', 'armeabi-v7a', 'x86_64'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }

    packagingOptions {
        resources.excludes += [
                'META-INF/LICENSE*',
                'META-INF/NOTICE*'
        ]
    }

    // Configure CMake build for JNI wrapper
    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
        }
    }

}

dependencies {
    implementation 'androidx.core:core-ktx:1.13.1'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'com.google.android.material:material:1.12.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
}
